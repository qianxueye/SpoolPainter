plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.ksp)
    alias(libs.plugins.hilt.android)
    alias(libs.plugins.kotlin.serialization)
}

android {
    namespace = "com.spoolpainter.app"
    compileSdk {
        version = release(36)
    }

    defaultConfig {
        applicationId = "cc.apture.spoolpainter.pos"
        minSdk = 29
        targetSdk = 36
        // 115 skipped past 114 deliberately: 114 was built for U22 as 2.3.2 but we
        // cannot verify whether it reached Play, and reusing a consumed code is
        // what got 113 rejected. Git tags are not a record of Play uploads.
        //
        // 116 / 2.4.1 — two bugfixes on top of 2.4.0 (UI-63 brand casing, UI-65
        // missing NFC status overlay on the no-Spoolman write). 115 is tagged and
        // GitHub-released, so it is treated as consumed regardless of whether it
        // reached Play. Confirm in Play Console before upload; skipping a code is
        // always safe, reusing one is not.
        versionCode = 116
        versionName = "2.4.1-pos.1"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        create("release") {
            val keystorePath = System.getenv("KEYSTORE_FILE") ?: System.getProperty("user.home") + "/spoolpainter-release-key.jks"
            val pwdFile = File(System.getProperty("user.home") + "/spoolpainter-keystore.pwd")
            val keystorePass = System.getenv("KEYSTORE_PASSWORD") ?: pwdFile.takeIf { it.exists() }?.readText()?.trim() ?: ""

            storeFile = file(keystorePath)
            storePassword = keystorePass
            keyAlias = "spoolpainter"
            keyPassword = keystorePass
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-DEBUG"
        }
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
            signingConfig = signingConfigs.getByName("release")
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    buildFeatures {
        compose = true
        buildConfig = true
    }
    testOptions {
        unitTests.isReturnDefaultValues = true
    }
}

dependencies {
    implementation("com.google.zxing:core:3.5.3")
    implementation("com.google.code.gson:gson:2.10.1")
    testImplementation("com.squareup.okhttp3:mockwebserver:4.12.0")
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.activity.compose)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.compose.ui)
    implementation(libs.androidx.compose.ui.graphics)
    implementation(libs.androidx.compose.ui.tooling.preview)
    implementation(libs.androidx.compose.material3)
    implementation(libs.androidx.compose.material.icons.extended)
    implementation("androidx.core:core-ktx:1.13.1")
    implementation("androidx.appcompat:appcompat:1.7.0")
    implementation("com.google.android.material:material:1.12.0")
    implementation("androidx.core:core-splashscreen:1.0.1")
    implementation("com.squareup.retrofit2:retrofit:2.9.0")
    implementation("com.squareup.retrofit2:converter-gson:2.9.0")
    implementation("com.squareup.okhttp3:logging-interceptor:4.12.0")

    // v2 — Hilt + KSP
    implementation(libs.hilt.android)
    ksp(libs.hilt.compiler)
    implementation(libs.hilt.navigation.compose)

    // v2 — Coroutines
    implementation(libs.kotlinx.coroutines.core)
    implementation(libs.kotlinx.coroutines.android)

    // v2 — Lifecycle Compose
    implementation(libs.androidx.lifecycle.viewmodel.compose)
    implementation(libs.androidx.lifecycle.runtime.compose)

    // v2 — DataStore + Serialization
    implementation(libs.androidx.datastore)
    implementation(libs.androidx.datastore.core)
    implementation(libs.kotlinx.serialization.json)

    // v2 — Datetime (U4: kotlinx.datetime.Clock for NfcRepository TTL)
    implementation(libs.kotlinx.datetime)

    // v2.2 — CameraX (U17: camera color sampler)
    implementation(libs.androidx.camera.core)
    implementation(libs.androidx.camera.camera2)
    implementation(libs.androidx.camera.lifecycle)
    implementation(libs.androidx.camera.view)

    testImplementation(libs.junit)
    testImplementation(libs.kotlinx.coroutines.test)
    testImplementation(libs.turbine)
    testImplementation(libs.mockk)
    // org.json is provided by Android at runtime but not on the JVM unit-test classpath.
    // Required by OpenSpoolPayloadCodec tests (U2).
    testImplementation("org.json:json:20231013")

    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.compose.ui.test.junit4)
    androidTestImplementation(libs.hilt.android.testing)
    kspAndroidTest(libs.hilt.compiler)

    debugImplementation(libs.androidx.compose.ui.tooling)
    debugImplementation(libs.androidx.compose.ui.test.manifest)
}
